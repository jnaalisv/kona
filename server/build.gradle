import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayInfoTask
import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
    id "java"
    //id "jacoco"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "org.springframework.boot" version "2.0.2.RELEASE"
}

apply from: "$rootDir/gradle/flyway-config.gradle"
apply from: "$rootDir/gradle/integration-test.gradle"
//apply from: "$rootDir/gradle/jacoco-config.gradle"
apply from: "$rootDir/gradle/javac-options.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    compile.exclude group: "commons-logging"
}

ext {
    dataSourceProxyVersion = "1.4.7"
}

dependencies {
    compile (
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-security",

            project(":domain"),

            "org.slf4j:slf4j-api",
            "com.zaxxer:HikariCP",
            "org.hibernate:hibernate-core",
            "org.springframework:spring-orm",

            "net.ttddyy:datasource-proxy:$dataSourceProxyVersion"
    )

    runtime "org.postgresql:postgresql"

    testCompile "junit:junit"
    testCompile "org.assertj:assertj-core"

    integrationTestCompile "org.springframework.boot:spring-boot-starter-test"
}

def integTestFlywayExtension;

task initIntegTestFlywayExtension() {
    integTestFlywayExtension = new FlywayExtension();
    integTestFlywayExtension.locations = ["filesystem:$rootDir/sql-migrations/schema"]
    integTestFlywayExtension.user = 'kona_test'
    integTestFlywayExtension.password = 'kona_test'
    integTestFlywayExtension.url = 'jdbc:postgresql://localhost:5432/kona_test'
}

task flywayInfoIntegTest(type: FlywayInfoTask, dependsOn: initIntegTestFlywayExtension) {
    extension = integTestFlywayExtension
}

task flywayMigrateIntegTest(type: FlywayMigrateTask, dependsOn: initIntegTestFlywayExtension) {
    extension = integTestFlywayExtension
}

integrationTest.dependsOn flywayMigrateIntegTest

