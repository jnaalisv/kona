import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayInfoTask
import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id "groovy"
    id "org.flywaydb.flyway" version "4.0.3"
    id "war"
    id "jacoco"
    id "org.akhikhl.gretty" version "1.4.0"
}

apply from: "$rootDir/gradle/flyway-config.gradle"
apply from: "$rootDir/gradle/integration-test.gradle"
apply from: "$rootDir/gradle/jacoco-config.gradle"
apply from: "$rootDir/gradle/javac-options.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

ext {
    slf4jVersion = "1.7.21"
    logbackVersion = "1.1.7"
    servletApiVersion = "3.1.0"
    springVersion = "4.3.4.RELEASE"
    springSecurityVersion = "4.1.3.RELEASE"
    jacksonVersion = "2.8.3"
    hibernateVersion = "5.2.4.Final"
    hikariCPVersion = "2.5.1"
    h2Version = "1.4.191"
    junitVersion = "4.12"
    assertJVersion = "3.5.2"
    groovyVersion = "2.4.7"
    postgreSqlVersion = "9.4.1211.jre7"
}

configurations {
    compile.exclude group: "commons-logging"
}

dependencies {

    compile (
        "org.slf4j:slf4j-api:$slf4jVersion",
        "com.zaxxer:HikariCP:$hikariCPVersion",
        "org.hibernate:hibernate-core:$hibernateVersion",
        "javax.inject:javax.inject:1",
        "org.springframework:spring-context:$springVersion",
        "org.springframework:spring-tx:$springVersion",
        "org.springframework:spring-orm:$springVersion",
        "javax.servlet:javax.servlet-api:$servletApiVersion",
        "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
        "org.springframework:spring-webmvc:$springVersion",
        "org.springframework.security:spring-security-web:$springSecurityVersion",
        "org.springframework.security:spring-security-core:$springSecurityVersion",
        "org.springframework.security:spring-security-config:$springSecurityVersion"
    )

    runtime (
        "org.slf4j:jcl-over-slf4j:$slf4jVersion",
        "org.codehaus.groovy:groovy:$groovyVersion",
        "ch.qos.logback:logback-classic:$logbackVersion",
        "org.postgresql:postgresql:$postgreSqlVersion"
    )

    testCompile (
        "junit:junit:$junitVersion",
        "org.assertj:assertj-core:$assertJVersion",
        "org.spockframework:spock-core:1.0-groovy-2.4"
    )

    integrationTestCompile (
            "org.springframework:spring-test:$springVersion"
    )
}

gretty {
    contextPath = "kona"
    servletContainer = "jetty9"
    debugSuspend = false
    integrationTestTask = 'none'
    port = 9999
}

def integTestFlywayExtension;

task initIntegTestFlywayExtension() {
    integTestFlywayExtension = new FlywayExtension();
    integTestFlywayExtension.locations = ["filesystem:$rootDir/sql-migrations/schema"]
    integTestFlywayExtension.user = 'kona_test'
    integTestFlywayExtension.password = 'kona_test'
    integTestFlywayExtension.url = 'jdbc:postgresql://localhost:5432/kona_test'
}

task flywayInfoIntegTest(type: FlywayInfoTask, dependsOn: initIntegTestFlywayExtension) {
    extension = integTestFlywayExtension
}

task flywayMigrateIntegTest(type: FlywayMigrateTask, dependsOn: initIntegTestFlywayExtension) {
    extension = integTestFlywayExtension
}

integrationTest.dependsOn flywayMigrateIntegTest

